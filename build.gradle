plugins {
    id 'org.springframework.boot' version '2.4.4'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'org.hidetake.ssh' version '2.10.1'
}

remotes {
    stage {
        host = '192.168.0.91'
        user = 'root'
        identity = file('C:/Users/TS/.ssh/id_rsa_ssh8')
    }
    production {
        host = '47.105.99.75'
        user = 'root'
        identity = file('C:/Users/TS/.ssh/id_rsa_ssh8')
    }
}

group = 'com.chenhongliang'
version = '1.0'
sourceCompatibility = JavaVersion.VERSION_1_8

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    maven {
        url 'https://maven.aliyun.com/repository/public/'
    }
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-freemarker'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:2.1.4'
    // https://mvnrepository.com/artifact/org.apache.httpcomponents/httpcore
    implementation group: 'org.apache.httpcomponents', name: 'httpcore', version: '4.4.9'
    // https://mvnrepository.com/artifact/org.apache.httpcomponents/httpclient
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.13'
    // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
    // https://mvnrepository.com/artifact/com.alibaba/fastjson
    implementation group: 'com.alibaba', name: 'fastjson', version: '1.2.76'
    // https://mvnrepository.com/artifact/com.github.pagehelper/pagehelper-spring-boot-starter
    implementation group: 'com.github.pagehelper', name: 'pagehelper-spring-boot-starter', version: '1.3.0'
    // https://mvnrepository.com/artifact/com.belerweb/pinyin4j
    implementation group: 'com.belerweb', name: 'pinyin4j', version: '2.5.1'
    // https://mvnrepository.com/artifact/commons-io/commons-io
    implementation group: 'commons-io', name: 'commons-io', version: '2.8.0'
    // https://mvnrepository.com/artifact/org.apache.poi/poi-ooxml
    implementation group: 'org.apache.poi', name: 'poi-ooxml', version: '5.0.0'
    // https://mvnrepository.com/artifact/org.apache.poi/poi-excelant
    implementation group: 'org.apache.poi', name: 'poi-excelant', version: '5.0.0'
    // https://mvnrepository.com/artifact/com.google.code.findbugs/annotations
    implementation group: 'com.google.code.findbugs', name: 'annotations', version: '3.0.1'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'mysql:mysql-connector-java'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

task deployStage(dependsOn: bootJar) {
    doLast {
        ssh.settings {
            knownHosts = allowAnyHosts
        }
        ssh.run {
            session(remotes.stage) {
                put from: "${buildDir}/libs/${rootProject.name}-${version}.jar", into: "/root/jars/${rootProject.name}.jar"
                put from: "${rootDir}/bin/config/${rootProject.name}.service", into: "/usr/lib/systemd/system/"
                put from: "${rootDir}/bin/config/${rootProject.name}.nginx.http.conf", into: "/etc/nginx/conf.d/"
                execute """
                    systemctl daemon-reload
                    systemctl enable ${rootProject.name}
                    systemctl restart ${rootProject.name}
                    systemctl restart nginx
                """
            }
        }
    }
}

task removeStage {
    doLast {
        ssh.settings {
            knownHosts = allowAnyHosts
        }
        ssh.run {
            session(remotes.stage) {
                execute """
                    systemctl stop ${rootProject.name}
                    systemctl disable ${rootProject.name}
                    rm /usr/lib/systemd/system/${rootProject.name}.service
                    systemctl daemon-reload
                    
                    rm /etc/nginx/conf.d/${rootProject.name}.nginx.http.conf
                    systemctl restart nginx
                    
                    rm /root/jars/${rootProject.name}.jar
                """
            }
        }
    }
}

task deployProduction(dependsOn: bootJar) {
    doLast {
        ssh.settings {
            knownHosts = allowAnyHosts
        }
        ssh.run {
            session(remotes.production) {
                put from: "${buildDir}/libs/${rootProject.name}-${version}.jar", into: "/root/jars/${rootProject.name}.jar"
                put from: "${rootDir}/bin/config/${rootProject.name}.service", into: "/usr/lib/systemd/system/"
                put from: "${rootDir}/bin/config/${rootProject.name}.nginx.http.conf", into: "/etc/nginx/conf.d/"
                execute """
                    systemctl daemon-reload
                    systemctl enable ${rootProject.name}
                    systemctl restart ${rootProject.name}
                    systemctl restart nginx
                """
            }
        }
    }
}

task removeProduction {
    doLast {
        ssh.settings {
            knownHosts = allowAnyHosts
        }
        ssh.run {
            session(remotes.production) {
                execute """
                    systemctl stop ${rootProject.name}
                    systemctl disable ${rootProject.name}
                    rm /usr/lib/systemd/system/${rootProject.name}.service
                    systemctl daemon-reload
                    
                    rm /etc/nginx/conf.d/${rootProject.name}.nginx.http.conf
                    systemctl restart nginx
                    
                    rm /root/jars/${rootProject.name}.jar
                """
            }
        }
    }
}

test {
    useJUnitPlatform()
}
